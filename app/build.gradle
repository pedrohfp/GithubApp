apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "br.com.githubapp"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

ext{
    support_version = "26.1.0"
    retrofit_version = "2.3.0"
    okhttp_version = "3.9.1"
    arch_version = "1.1.0"
    kodein_version = "4.1.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"
    implementation "com.github.salomonbrys.kodein:kodein:$kodein_version"
    implementation "com.github.salomonbrys.kodein:kodein-android:$kodein_version"
    implementation 'com.squareup.picasso:picasso:2.5.2'

    //Unit Tests
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.7"
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0', { exclude group: 'org.jetbrains.kotlin' }
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version", { exclude group: 'com.squareup.okio' }
    testImplementation "android.arch.core:core-testing:$arch_version"

    //Integration Tests
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //Linters
    ktlint "com.github.shyiko:ktlint:0.15.0"

    //Processor
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
